# vim: filetype=csh
unsetenv ANF

setenv ANF /opt/anf/$id

# Set some variables to dummy valies so that the Antelope setup script will add
# it's path elements. We'll fix the ordering below.

# PATH (path), MANPATH, and PFPATH are always set by setup.sh even if empty
# SCHEMA_DIR is weird - no processing is done for 5.4 and older in Antelope
# setup.sh. Leaving it set to a dummy value will result in a broken Antelope.

if (! $?DATAPATH ) then 
  setenv DATAPATH $ANF/data
endif

# include the Antelope setup
source /opt/antelope/$id/setup.csh


set a="$ANF/bin"

foreach i ( $path )
  if ( "$i" !~ /opt/anf/* ) then
    set a=($a $i)
  endif
end
set path=($a)

set a=$ANF/man
foreach i ( `echo $MANPATH | sed 's/:/ /g'` )
  if ( "$i" !~ /opt/anf/* ) then
    set a=($a $i)
  endif
end
setenv MANPATH `echo $a | sed 's/ /:/g' `

set a=( )
set b=( $ANF/data/pf ./pf . )
foreach i ( `echo $PFPATH | sed 's/:/ /g'` )
  if ( "$i" !~ /opt/anf/* && "$i" != '.' && "$i" != './pf' ) then
    set a=($a $i)
  endif
end
set a=($a $b)
setenv PFPATH `echo $a | sed 's/ /:/g' `

set a=()
set b=($ANF/data)
foreach i ( `echo $DATAPATH | sed 's/:/ /g'` )
  if ( "$i" !~ /opt/anf/* ) then
    set a=($a $i)
  endif
end
set a=($a $b)
setenv DATAPATH `echo $a | sed 's/ /:/g' `

if ( -d $ANF/data/schemas ) then
  if ( $?SCHEMA_DIR ) then
    set a=($ANF/data/schemas)
    foreach i ( `echo $SCHEMA_DIR | sed 's/:/ /g'` )
      if ( "$i" !~ /opt/anf/* && ) then
        set a=($a $i)
      endif
    end
    setenv SCHEMA_DIR `echo $a | sed 's/ /:/g' `
  else
    # In order to override schema_dir, we must provide sane defaults. Can't
    # assume Antelope has a sane fallback for 5.4 and older.
    # Antelope 5.5 will always set SCHEMA_DIR so we don't hit this block
    set a=( $ANF/data/schemas $ANTELOPE/data/schemas )
    if ( -d $ANTELOPE/contrib/data/pf ) then
      set a=($a $ANTELOPE/contrib/data/pf)
    endif
    if ( -d $ANTELOPE/local/data/pf ) then
      set a=($a $ANTELOPE/local/data/pf)
    endif
    setenv SCHEMADIR `echo $a | sed 's/ /:/g' `
  endif
endif

unset a
unset b
unset m
unset p

setenv ANFMAKE     $ANF/include/anfmake

set anf=$ANF
set antelopemake=$ANTELOPEMAKE
